#!/usr/bin/env rune

pragma.syntax("0.8")

# Copyright 2002 Combex, Inc. under the terms of the MIT X license
# found at http://www.opensource.org/licenses/mit-license.html ................

def EExpr := <type:org.erights.e.elang.evm.EExpr>
def Expr := any[EExpr, Twine, <type:org.quasiliteral.term.Term>]

def makeTextWriter := <elib:oldeio.makeTextWriter>
def cmdMakerMaker := <elang:cmd.cmdMakerMaker>
def makeEvalContextAuthor := <elang:cmd.makeEvalContextAuthor>

def makeEvaluatorAuthor(gc, auths, options, vat) :any {

    def makeEvalContext := makeEvalContextAuthor(auths,
                                                 options,
                                                 vat,
                                                 false,      #confined
                                                 true)       #explicitIO

    def cmdLoopMaker := <elang:cmd.cmdLoopMakerAuthor>(gc)

    def makeEvaluator {
        to run(args, props) :any {

            def [metaout, metaoutBuf] := makeTextWriter.makeBufferingPair()

            def cmdLoop
            def evalContext := makeEvalContext(props, cmdLoop)
            bind cmdLoop := cmdLoopMaker(args,
                                         props,
                                         evalContext.getTopScope(),
                                         true)

            def cmdMaker := cmdMakerMaker(cmdLoop,
                                          evalContext.getOptAltoutBuf(),
                                          evalContext.getOptAlterrBuf(),
                                          metaout,
                                          metaout)

            def evalPrint(expr :Expr) :vow[boolean] {
                def [exitStatusVow, cmd] :=
                  cmdMaker.makeCmdAnswerer(expr)
                if (cmdLoop.enqueue(cmd)) {
                    exitStatusVow
                } else {
                    cmdLoop.getFinalExitStatusVow()
                }
            }

            def evaluator {

                /**
                 * Schedules expr to be evaluated as the next top level
                 * command in the evaluator's current scope, and returns a
                 * vow for a textual report of the outcome
                 */
                to evaluateForReport(expr :Expr) :vow[Tuple[String, boolean]] {
                    when (evalPrint(expr)) ->
                      done(exitStatus :boolean) :Tuple[String, boolean] {

                        def transcript := metaoutBuf.snapshot()
                        metaoutBuf.setLength(0)
                        [transcript, exitStatus]
                    } catch problem {
                        throw(problem)
                    }
                }

                to getCmdLoop() :any { cmdLoop }
            }
        }
        to shutdown() :void {
            vat.orderlyShutdown("makeEvaluator shutdown")
        }
    }
}
